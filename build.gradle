import java.text.SimpleDateFormat

buildscript {
	ext {
		kotlinVersion = '1.4.10'
        kotlinCoroutineVersion = '1.4.2'
        springBootVersion = '2.4.0'
		springVersion = '5.2.7.RELEASE'
        dokkaVersion = '1.4.10.2'
	}
	repositories {
		mavenCentral()
		maven { url = 'https://maven.taktik.be/content/groups/public' }
		maven { url = 'https://repo.spring.io/plugins-release' }
        gradlePluginPortal()
        jcenter()
	}
	dependencies {
		classpath('io.spring.gradle:propdeps-plugin:0.0.9.RELEASE')
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath 'com.taktik.gradle:gradle-plugin-docker-java:2.0.6'

		classpath('com.taktik.gradle:gradle-plugin-git-version:1.0.13')
		classpath('com.github.ben-manes:gradle-versions-plugin:0.21.0')
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}")
    }
}

apply plugin: 'git-version'
group = 'org.taktik.icure'
version = gitVersion ?: '2.0.1-SNAPSHOT'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker-java'
apply plugin: 'com.github.ben-manes.versions' // usage: ./gradlew dependencyUpdates
apply plugin: 'maven-publish'

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

compileJava {
    options.fork = true
    options.forkOptions.with {
        memoryMaximumSize = "4096m"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

repositories {
	mavenCentral()
    maven {
        url = "https://www.e-contract.be/maven2/"
    }
    maven {
        url = "http://repo.ehealth.fgov.be/artifactory/maven2/"
    }
    maven {
        url = "https://maven.taktik.be/content/groups/public"
    }
	maven { url = 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' } // required for swagger 3 snapshot, remove it once it is published
	flatDir {
        dirs 'libs'
    }
}


bootJar {
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision' : gitVersion,
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

dependencies {
    api("com.github.pozo:mapstruct-kotlin:1.3.1.2")
    kapt("com.github.pozo:mapstruct-kotlin-processor:1.3.1.2")

    optional group: 'org.springframework.boot', name:'spring-boot-configuration-processor'

    implementation group: 'org.springframework.boot', name:'spring-boot-starter-mail', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name:'spring-boot-starter-webflux', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name:'spring-boot-starter-security', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name:'spring-boot-starter-actuator', version:"${springBootVersion}"

	implementation group: 'com.fasterxml.jackson.module', name:'jackson-module-kotlin', version:'2.11.3'
    implementation group: 'com.fasterxml.jackson.core', name:'jackson-databind', version:'2.11.3'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: '2.11.3'

    implementation group: 'org.hibernate.validator', name:'hibernate-validator', version:'6.1.5.Final'
    implementation group: 'org.hibernate.validator', name:'hibernate-validator-annotation-processor', version:'6.1.5.Final'
    implementation group: 'org.hibernate.validator', name:'hibernate-validator-cdi', version:'6.1.5.Final'

    implementation group: 'javax.el', name:'javax.el-api', version:'3.0.0'
    implementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: kotlinCoroutineVersion
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-reactor', version: kotlinCoroutineVersion
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-collections-immutable-jvm', version: '0.3'

    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.8.0'

    implementation group: 'org.springframework.session', name: 'spring-session-core', version: '2.2.1.RELEASE'

	implementation group: 'org.springframework', name: 'spring-aspects', version: springVersion
    implementation group: 'org.springframework', name: 'spring-websocket', version: springVersion
    implementation group: 'org.springframework', name: 'spring-orm', version: springVersion

    // Logging
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'ch.qos.logback', name: 'logback-access', version: '1.2.3'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.12'
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.12'
    implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.12'

    // Java Commons
    implementation group: 'org.taktik.commons', name: 'commons-uti', version: '1.0'
    compile(group: 'org.taktik.commons', name: 'commons-service', version: '1.0') {
        exclude group: 'org.taktik.commons', module: 'commons-cluster'
        exclude group: 'org.taktik.commons', module: 'commons-utils'
    }
    implementation group: 'org.taktik.couchdb', name: 'krouch', version: 'jack211-1.0.2-28-g7676e8658c'

	// APIs
	implementation group: 'javax.annotation', name: 'jsr250-api', version: '1.0'
	implementation group: 'org.apache.ws.commons.axiom', name: 'axiom-impl', version: '1.2.15'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

	// Others
	implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
	implementation group: 'org.ow2.asm', name: 'asm', version: '5.0.4'
	implementation group: 'org.ow2.asm', name: 'asm-commons', version: '5.0.4'
	implementation group: 'com.google.guava', name: 'guava', version: '20.0'
	implementation group: 'joda-time', name: 'joda-time', version: '2.9.7'
	implementation group: 'org.jdom', name: 'jdom-legacy', version: '1.1.3'
	implementation group: 'commons-io', name: 'commons-io', version: '2.5'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
	implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.12'
	implementation group: 'com.eaio.uuid', name: 'uuid', version: '3.2'
	implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.2'
	implementation group: 'com.fasterxml.uuid', name: 'java-uuid-generator', version: '3.1.3'
	implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.2'
	implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
    implementation group: 'com.sendgrid', name: 'sendgrid-java', version: '4.4.7'
    implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.3.1.Final'
	implementation group: 'org.apache.axis2', name: 'axis2-kernel', version: '1.5.6'
	implementation group: 'org.apache.axis2', name: 'axis2-adb', version: '1.5.6'

	implementation group: 'org.apache.lucene', name: 'lucene-core', version: '4.10.4'
	implementation group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '4.10.4'


	// Bouncy Castle
	implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.53'
	implementation group: 'org.bouncycastle', name: 'bcmail-jdk15on', version: '1.53'

	//2FA
	implementation group:'org.jboss.aerogear', name:'aerogear-otp-java', version:'1.0.0'

	// Swagger
    implementation "org.springdoc:springdoc-openapi-webflux-ui:1.3.0"
    implementation "org.springdoc:springdoc-openapi-kotlin:1.3.0"

	//Saxon
	implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: '9.6.0-6'

	//DTO check
	implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '2.0.0'

	//JSP stuff
	implementation group: 'org.apache.taglibs', name: 'taglibs-standard-spec', version: '1.2.5'
	implementation group: 'org.apache.taglibs', name: 'taglibs-standard-impl', version: '1.2.5'

	//EH Validator
	implementation group: 'com.ibm.icu', name: 'icu4j', version: '57.1'
	implementation name: 'ehvalidator-service-core-2.1.1'

    // mustache
    implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.5'

    implementation group: 'javax.activation', name: 'activation', version: '1.1'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'

	// Runtime
	runtime group: 'org.javassist', name: 'javassist', version: '3.18.1-GA'

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

compileJava.dependsOn(processResources)

// Setup resolution strategy
configurations.all {
    resolutionStrategy {
        // Don't cache changing modules at all
        cacheChangingModulesFor 0, 'seconds'
        force 'junit:junit:4.11'
        force group: 'net.sf.jsignature.io-tools', name: 'easystream', version: '1.2.12'
    }

    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    exclude group: 'com.sun.security', module: 'pkcs11'
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'org.mortbay.jetty', module: 'jetty'
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
    exclude group: 'log4j', module: 'apache-log4j-extras'
    exclude group: 'asm', module: 'asm'
    exclude group: 'javax', module: 'javaee-api'
    exclude group: 'org.apache.openejb', module: 'javaee-api'
}

springBoot {
	mainClassName = "org.taktik.icure.ICureBackendApplicationKt"
}

bootRun {
   if ( project.hasProperty('jvmArgs') ) {
        jvmArgs = (project.jvmArgs.split("\\s+") as List)
   }
}

// Write the full name of the dependency to the standard output
project.tasks.withType(AbstractPublishToMaven) {
    doFirst {
        println "PublishToMaven: " + project.group + ":" + project.name + ":" + project.version
    }
}

publish {
    dependsOn assemble
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url "https://maven.taktik.be/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            credentials {
                username repoUsername
                password repoPassword
            }
        }
    }
}

dockerJava {
    tag = 'taktik/icure-backend'
}
